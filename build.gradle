import dependencies.Libs

buildscript {
    repositories {
        google()
        mavenCentral()
        maven{
            url "https://maven.pkg.jetbrains.space/public/p/compose/dev"
        }
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.plugin
        classpath Libs.composeGradlePlugin
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.12.4'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven{
            url "https://maven.pkg.jetbrains.space/public/p/compose/dev"
        }
    }
}

subprojects {
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint("0.41.0")
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach { compile ->
        kotlinOptions {
            // Set JVM target to 1.8
            jvmTarget = "1.8"
            // Allow use of @OptIn
            freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        }
    }
}
